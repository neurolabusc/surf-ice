//pref
Ambient|float|0.0|0.5|1
Diffuse|float|0.0|0.7|1
Specular|float|0.0|0.2|1
Shininess|float|1|60|120
WireWidth|float|0.1|0.7|24
WireColor|float|0|0.2|1
Wireframe useful for evaluating mesh quality. Copyright 2015 Chris Rorden, BSD2clause.|note
//vert
#version 330
layout(location = 0) in vec3 Vert;
layout(location = 3) in vec3 Norm;
layout(location = 6) in vec4 Clr;
out vec3 vN, vL, vV;
out vec4 vPmvp, vClr, vP;
uniform mat4 ModelViewProjectionMatrix;
uniform mat4 ModelViewMatrix;
uniform mat3 NormalMatrix;
uniform vec3 LightPos = vec3(0.0, 20.0, 30.0); //LR, -DU+, -FN+
void main() {
    vN = normalize((NormalMatrix * Norm));
    vP = vec4(Vert, 1.0);
    gl_Position = ModelViewProjectionMatrix * vec4(Vert, 1.0);
    vPmvp = gl_Position;
    vL = normalize(LightPos);
    vV = -vec3(ModelViewMatrix*vec4(Vert,1.0));
    vClr = Clr;
}
//geom
#version 330
in vec3 vL[3];   //[3] because this makes a triangle
in vec3 vN[3];
in vec3 vV[3];
in vec4 vP[3];
in vec4 vPmvp[3];
in vec4 vClr[3];
layout (triangles) in;
layout (triangle_strip, max_vertices = 3) out;

out vec3 gE; //Edge value
out vec3 gL;
out vec3 gN;
out vec3 gV;
out vec4 gP;
out vec4 gClr;

void main(void) {
    for(int i=0; i < 3; i++) {
    	gl_Position = vPmvp[i];
    	if (i == 0)
    		gE = vec3(1.0, 0.0, 0.0);
    	else if (i == 1)
    		gE = vec3(0.0, 1.0, 0.0);
    	else
    		gE = vec3(0.0, 0.0, 1.0);
    	gL = vL[i];
    	gN = vN[i];
    	gV = vV[i];
    	gP = vP[i];
    	gClr = vClr[i];
        EmitVertex();
    }
    EndPrimitive();
}
//frag
#version 330
uniform float Ambient, Diffuse, Specular, Shininess, WireColor, WireWidth;
uniform vec4 ClipPlane;
in vec3 gE, gL, gN, gV;
in vec4 gP, gClr;
out vec4 color;

float edgeFactor(){
    vec3 d = fwidth(gE);
    vec3 a3 = smoothstep(vec3(0.0), d*WireWidth, gE);
    return min(min(a3.x, a3.y), a3.z);
}

void main() {
	if ((ClipPlane[0] < 1.5) && (dot( ClipPlane, gP) > 0.0)) discard;
	vec3 l = normalize(gL);
	vec3 n = normalize(gN);
	vec3 h = normalize(l+normalize(gV));
	vec3 a = gClr.rgb;
	vec3 d = a * Diffuse;
	a *= Ambient;
	float diff = dot(n,l);
	float spec = pow(max(0.0,dot(n,h)), Shininess);
	vec3 backcolor = Ambient*vec3(0.1+0.1+0.1) + d*abs(diff);
	float backface = step(0.00, n.z);
	vec4 face = vec4(mix(backcolor.rgb, a + d*diff + spec*Specular,  backface), 1.0);
	vec4 wire = vec4(WireColor, WireColor, WireColor, 1.0 - edgeFactor() );
	face.rgb = mix(face.rgb, wire.rgb, wire.a);
	face.a = max(face.a, wire.a);
	color = face;
}
// Solid Wireframe, NVIDIA Whitepaper WP-03014-001_v01
// http://codeflow.org/entries/2012/aug/02/easy-wireframe-display-with-barycentric-coordinates/
// http://www.imm.dtu.dk/~janba/Wireframe/
